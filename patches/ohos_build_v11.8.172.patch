diff --git a/build_config.h b/build_config.h
index 248470369..a52c1bcb6 100644
--- a/build_config.h
+++ b/build_config.h
@@ -60,6 +60,8 @@
 #define OS_NACL 1
 #elif defined(ANDROID)
 #define OS_ANDROID 1
+#elif defined(OSOHOS)
+#define OS_OHOS 1
 #elif defined(__APPLE__)
 // Only include TargetConditionals after testing ANDROID as some Android builds
 // on the Mac have this header available and it's not needed unless the target
@@ -129,7 +131,7 @@
     defined(OS_FREEBSD) || defined(OS_IOS) || defined(OS_LINUX) ||  \
     defined(OS_CHROMEOS) || defined(OS_MAC) || defined(OS_NACL) ||  \
     defined(OS_NETBSD) || defined(OS_OPENBSD) || defined(OS_QNX) || \
-    defined(OS_SOLARIS) || defined(OS_ZOS)
+    defined(OS_SOLARIS) || defined(OS_ZOS) || defined(OS_OHOS)
 #define OS_POSIX 1
 #endif
 
@@ -373,7 +375,7 @@
 #error Please add support for your compiler in build/build_config.h
 #endif
 
-#if defined(OS_ANDROID)
+#if defined(OS_ANDROID) || defined(OS_OHOS)
 // The compiler thinks std::string::const_iterator and "const char*" are
 // equivalent types.
 #define STD_STRING_ITERATOR_IS_CHAR_POINTER
diff --git a/config/BUILD.gn b/config/BUILD.gn
index 53511ac36..bc60746e9 100644
--- a/config/BUILD.gn
+++ b/config/BUILD.gn
@@ -318,7 +318,7 @@ config("executable_config") {
       "//build/config/ios:ios_dynamic_flags",
       "//build/config/ios:ios_executable_flags",
     ]
-  } else if (is_linux || is_chromeos || is_android || current_os == "aix") {
+  } else if (is_linux || is_chromeos || is_android || current_os == "aix" || is_ohos) {
     configs += [ "//build/config/gcc:executable_config" ]
     if (is_castos || is_cast_android) {
       configs += [ "//build/config/chromecast:executable_config" ]
diff --git a/config/BUILDCONFIG.gn b/config/BUILDCONFIG.gn
index 105e194dd..dc6d109c4 100644
--- a/config/BUILDCONFIG.gn
+++ b/config/BUILDCONFIG.gn
@@ -51,8 +51,8 @@ if (target_os == "") {
 }
 
 if (target_cpu == "") {
-  if (target_os == "android") {
-    # If we're building for Android, we should assume that we want to
+  if (target_os == "android" || target_os == "ohos") {
+    # If we're building for Android or OHOS, we should assume that we want to
     # build for ARM by default, not the host_cpu (which is likely x64).
     # This allows us to not have to specify both target_os and target_cpu
     # on the command line.
@@ -235,6 +235,10 @@ _default_toolchain = ""
 if (target_os == "android") {
   assert(host_os == "linux", "Android builds are only supported on Linux.")
   _default_toolchain = "//build/toolchain/android:android_clang_$target_cpu"
+} else if (target_os == "ohos") {
+  assert(host_os == "linux" || host_os == "mac",
+         "ohos builds are only supported on Linux and Mac hosts.")
+  _default_toolchain = "//build/toolchain/ohos:ohos_clang_$target_cpu"
 } else if (target_os == "chromeos" || target_os == "linux") {
   # See comments in build/toolchain/cros/BUILD.gn about board compiles.
   if (is_clang) {
@@ -306,6 +310,11 @@ is_linux = current_os == "linux"
 is_mac = current_os == "mac"
 is_nacl = current_os == "nacl"
 is_win = current_os == "win" || current_os == "winuwp"
+is_ohos = current_os == "ohos"
+
+declare_args() {
+  use_musl = false
+}
 
 is_apple = is_ios || is_mac
 is_posix = !is_win && !is_fuchsia
@@ -380,7 +389,7 @@ if (is_fuchsia) {
   default_compiler_configs += [ "//build/config/gcc:symbol_visibility_hidden" ]
 }
 
-if (is_android) {
+if (is_android || is_ohos) {
   default_compiler_configs +=
       [ "//build/config/android:default_orderfile_instrumentation" ]
 }
@@ -554,7 +563,7 @@ foreach(_target_type,
       # On Android, write shared library output file to metadata. We will use
       # this information to, for instance, collect all shared libraries that
       # should be packaged into an APK.
-      if (!defined(invoker.metadata) && (is_android || is_robolectric) &&
+      if (!defined(invoker.metadata) && (is_android || is_ohos || is_robolectric) &&
           (_target_type == "shared_library" ||
            _target_type == "loadable_module")) {
         _output_name = _target_name
diff --git a/config/android/BUILD.gn b/config/android/BUILD.gn
index e7c18c068..608db1175 100644
--- a/config/android/BUILD.gn
+++ b/config/android/BUILD.gn
@@ -11,7 +11,7 @@ if (current_toolchain == default_toolchain) {
   import("//build/toolchain/concurrent_links.gni")
 }
 
-assert(is_android)
+assert(is_android || is_ohos)
 
 # This is included by reference in the //build/config/compiler config that
 # is applied to all targets. It is here to separate out the logic that is
diff --git a/config/android/abi.gni b/config/android/abi.gni
index 1a082cf2b..e7a35e07c 100644
--- a/config/android/abi.gni
+++ b/config/android/abi.gni
@@ -8,7 +8,7 @@
 # NOTE: Because Chrome OS builds may depend on targets built with the Android
 # toolchain, this GNI file may be read and processed from within Chrome OS
 # toolchains. Checking |is_android| here would therefore be too restrictive.
-assert(is_android || is_chromeos)
+assert(is_android || is_chromeos || is_ohos)
 
 declare_args() {
   # Adds intrumentation to each function. Writes a file with the order that
diff --git a/config/android/config.gni b/config/android/config.gni
index d8d9a6b8d..a4c4e168c 100644
--- a/config/android/config.gni
+++ b/config/android/config.gni
@@ -21,7 +21,7 @@ declare_args() {
 # NOTE: Because Chrome OS builds may depend on targets built with the Android
 # toolchain, this GNI file may be read and processed from within Chrome OS
 # toolchains. Checking |is_android| here would therefore be too restrictive.
-if (is_android || is_chromeos) {
+if (is_android || is_chromeos || is_ohos) {
   import("//build/config/android/channel.gni")
   import("//build/config/chromecast_build.gni")
   import("//build/config/clang/clang.gni")
diff --git a/config/android/rules.gni b/config/android/rules.gni
index 40be6fb60..dc0c11487 100644
--- a/config/android/rules.gni
+++ b/config/android/rules.gni
@@ -16,7 +16,7 @@ import("//build/config/rts.gni")
 import("//build/config/sanitizers/sanitizers.gni")
 import("//build/config/zip.gni")
 import("//build/toolchain/toolchain.gni")
-assert(is_android || is_robolectric)
+assert(is_android || is_robolectric || is_ohos)
 
 if (target_cpu == "arm") {
   _sanitizer_arch = "arm"
diff --git a/config/arm.gni b/config/arm.gni
index 5b404c1ca..fb30513bf 100644
--- a/config/arm.gni
+++ b/config/arm.gni
@@ -137,7 +137,7 @@ if (current_cpu == "arm" || v8_current_cpu == "arm") {
     # - "none": No branch protection.
     arm_control_flow_integrity = "none"
 
-    if ((is_android || is_linux) && target_cpu == "arm64") {
+    if ((is_android || is_linux || is_ohos) && target_cpu == "arm64") {
       # Enable PAC and BTI on AArch64 Linux/Android systems.
       # target_cpu == "arm64" filters out some cases (e.g. the ChromeOS x64
       # MSAN build) where the target platform is x64, but V8 is configured to
diff --git a/config/c++/c++.gni b/config/c++/c++.gni
index 3c0b549b2..b4b72c3d4 100644
--- a/config/c++/c++.gni
+++ b/config/c++/c++.gni
@@ -13,7 +13,7 @@ declare_args() {
   # Don't check in changes that set this to false for more platforms; doing so
   # is not supported.
   use_custom_libcxx = is_fuchsia || is_android || is_apple || is_linux ||
-                      is_chromeos || (is_win && is_clang)
+                      is_chromeos || (is_win && is_clang) || is_ohos
 
   # Use libc++ instead of stdlibc++ when using the host_cpu toolchain, even if
   # use_custom_libcxx is false. This is useful for cross-compiles where a custom
@@ -76,7 +76,7 @@ export_libcxxabi_from_executables =
 # the Bionic dynamic loader doesn't even look at RPATH; instead, LD_LIBRARY_PATH
 # is set for tests. Because of this, we make libc++ a shared library on android
 # since it should get loaded properly.
-if (is_android && export_libcxxabi_from_executables) {
+if ((is_android || is_ohos) && export_libcxxabi_from_executables) {
   export_libcxxabi_from_executables = false
   libcxx_is_shared = true
 }
diff --git a/config/clang/BUILD.gn b/config/clang/BUILD.gn
index fe044e29e..0ef290f03 100644
--- a/config/clang/BUILD.gn
+++ b/config/clang/BUILD.gn
@@ -32,7 +32,7 @@ config("find_bad_constructs") {
       "check-stack-allocated",
     ]
 
-    if (is_linux || is_chromeos || is_android || is_fuchsia) {
+    if (is_linux || is_chromeos || is_android || is_fuchsia || is_ohos) {
       cflags += [
         "-Xclang",
         "-plugin-arg-find-bad-constructs",
diff --git a/config/ohos/BUILD.gn b/config/ohos/BUILD.gn
new file mode 100644
index 000000000..d92230a93
--- /dev/null
+++ b/config/ohos/BUILD.gn
@@ -0,0 +1,306 @@
+# Copyright 2022 The Huawei Authors. All rights reserved.
+# Use of this source code is governed by a BSD-style license that can be
+# found in the LICENSE file.
+
+import("//build/config/c++/c++.gni")
+import("//build/config/compiler/compiler.gni")
+import("//build/config/ohos/config.gni")
+import("//build/config/sanitizers/sanitizers.gni")
+
+assert(is_ohos)
+
+# This is included by reference in the //build/config/compiler config that
+# is applied to all targets. It is here to separate out the logic that is
+# ohos-only.
+config("compiler") {
+  cflags = [
+    "-ffunction-sections",
+    "-fno-short-enums",
+  ]
+  defines = [
+    "OSOHOS",
+
+    # The NDK has these things, but doesn't define the constants to say that it
+    # does. Define them here instead.
+    "HAVE_SYS_UIO_H",
+  ]
+
+  if (use_musl) {
+    defines += [
+      "__MUSL__",
+      "_LIBCPP_HAS_MUSL_LIBC",
+      "__BUILD_LINUX_WITH_CLANG",
+    ]
+    if (current_cpu == "x64") {
+      defines += [ "PTRACE_GET_THREAD_AREA=25" ]
+    }
+  } else {
+    defines += [ "__BUILD_LINUX_WITH_CLANG" ]
+  }
+
+  ldflags = [
+    "-Wl,--exclude-libs=libgcc.a",
+    "-Wl,--no-undefined",
+    "-Wl,--exclude-libs=libunwind_llvm.a",
+    "-Wl,--exclude-libs=libc++_static.a",
+
+    # Don't allow visible symbols from libraries that contain
+    # assembly code with symbols that aren't hidden properly.
+    # http://crbug.com/448386
+    "-Wl,--exclude-libs=libvpx_assembly_arm.a",
+  ]
+
+  # TODO(crbug.com/1184398): Move to compiler-rt when we are ready.
+  if (use_musl) {
+    if (current_cpu == "arm") {
+    } else {
+      # ldflags += [ "--rtlib=libgcc" ]
+    }
+  }
+
+  if (current_cpu == "arm64") {
+    # For outline atomics on AArch64 (can't pass this unconditionally
+    # due to unused flag warning on other targets).
+    # cflags += [ "--rtlib=libgcc" ]
+  }
+
+  # $compile_api_level corresponds to the API level used for the sysroot path
+  # calculation in //build/config/ohos/config.gni
+  if (current_cpu == "arm") {
+    abi_target = "arm-linux-androideabi"
+    if (use_musl) {
+      abi_target = "arm-linux-ohosmusl"
+    } else {
+      compile_api_level = arm32_ndk_api_level
+    }
+  } else if (current_cpu == "x86") {
+    abi_target = ""
+    compile_api_level = ""
+  } else if (current_cpu == "arm64") {
+    abi_target = "aarch64-linux-ohos"
+    if (!use_musl) {
+      abi_target = "aarch64-linux-android"
+      compile_api_level = arm64_ndk_api_level
+    }
+  } else if (current_cpu == "x64") {
+    abi_target = "x86_64-linux-ohos"
+  } else {
+    assert(false, "Architecture not supported")
+  }
+
+  if (use_musl) {
+    cflags += [ "--target=$abi_target" ]
+    if (use_custom_libcxx) {
+      include_dirs = [ "$ohos_sysroot/usr/include/$abi_target" ]
+    } else {
+      include_dirs = [ "$ohos_toolchain_root/include/libcxx-ohos/include/c++/v1", "$ohos_sysroot/usr/include" ]
+    }
+    if (current_cpu == "x64") {
+      include_dirs += [ "$ohos_sysroot/usr/include/x86_64-linux-ohos/asm-x86" ]
+    }
+    ldflags += [ "--target=$abi_target" ]
+  } else {
+    cflags += [
+      "--target=$abi_target",
+      "-D__ANDROID_API__=$compile_api_level",
+    ]
+    ldflags += [ "--target=$abi_target" ]
+    include_dirs = [
+      "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/sysroot/usr/include",
+      "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/sysroot/usr/include/$abi_target",
+    ]
+  }
+
+  # Assign any flags set for the C compiler to asmflags so that they are sent
+  # to the assembler.
+  asmflags = cflags
+}
+
+# This is included by reference in the //build/config/compiler:runtime_library
+# config that is applied to all targets. It is here to separate out the logic
+# that is Android-only. Please see that target for advice on what should go in
+# :runtime_library vs. :compiler.
+config("runtime_library") {
+  libs = []
+  ldflags = []
+
+  if (use_musl) {
+    defines = [
+      "__MUSL__",
+      "_LIBCPP_HAS_MUSL_LIBC",
+      "__BUILD_LINUX_WITH_CLANG",
+    ]
+  } else {
+    defines = [ "__BUILD_LINUX_WITH_CLANG" ]
+  }
+  if (use_musl) {
+    ldflags += [ "-Wl,--dynamic-linker,/lib/ld-musl-arm.so.1" ]
+
+    if (current_cpu == "arm") {
+      ldflags += [ "-L" + rebase_path("$ohos_sysroot/usr/lib/arm-linux-ohos",
+                                      root_build_dir) ]
+      ldflags +=
+          [ "-L" + rebase_path("$ohos_toolchain_root/lib/arm-linux-ohos/c++",
+                               root_build_dir) ]
+      ldflags +=
+          [ "-L" + rebase_path(
+                "$ohos_toolchain_root/lib/clang/15.0.4/lib/arm-linux-ohos",
+                root_build_dir) ]
+      libclang_rt_file = "$ohos_toolchain_root/lib/clang/15.0.4/lib/arm-linux-ohos/libclang_rt.builtins.a"
+    }
+
+    if (current_cpu == "arm64") {
+      ldflags += [ "-Wl,-z,max-page-size=4096" ]
+      ldflags += [ "-Wl,--hash-style=gnu" ]
+      ldflags +=
+          [ "-L" + rebase_path("$ohos_sysroot/usr/lib/aarch64-linux-ohos",
+                               root_build_dir) ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_toolchain_root/lib/aarch64-linux-ohos/c++",
+                       root_build_dir) ]
+      ldflags +=
+          [ "-L" + rebase_path(
+                "$ohos_toolchain_root/lib/clang/15.0.4/lib/aarch64-linux-ohos",
+                root_build_dir) ]
+      libclang_rt_file = "$ohos_toolchain_root/lib/clang/15.0.4/lib/aarch64-linux-ohos/libclang_rt.builtins.a"
+
+      if (is_asan) {
+        libasan_file = "$ohos_toolchain_root/lib/clang/15.0.4/lib/aarch64-linux-ohos/libclang_rt.asan.so"
+        libs += [ rebase_path(libasan_file) ]
+      }
+    }
+
+    if (current_cpu == "x64") {
+      ldflags += [ "-Wl,-z,max-page-size=4096" ]
+      ldflags += [ "-Wl,--hash-style=gnu" ]
+      ldflags += [ "-L" + rebase_path("$ohos_sysroot/usr/lib/x86_64-linux-ohos",
+                                      root_build_dir) ]
+      ldflags +=
+          [ "-L" + rebase_path("$ohos_toolchain_root/lib/x86_64-linux-ohos/c++",
+                               root_build_dir) ]
+      ldflags +=
+          [ "-L" + rebase_path(
+                "$ohos_toolchain_root/lib/clang/15.0.4/lib/x86_64-linux-ohos",
+                root_build_dir) ]
+      libclang_rt_file = "$ohos_toolchain_root/lib/clang/15.0.4/lib/x86_64-linux-ohos/libclang_rt.builtins.a"
+    }
+
+    libs += [
+      rebase_path(libclang_rt_file),
+      "c",
+      "c++",
+      "c++abi",
+    ]
+  } else {
+    cflags = [
+      "-isystem" + rebase_path(
+              "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/libcxx/include",
+              root_build_dir),
+      "-isystem" + rebase_path(
+              "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/libcxxabi/include",
+              root_build_dir),
+      "-isystem" + rebase_path("$ohos_toolchain_root/lib64/clang/9.0.3/include",
+                               root_build_dir),
+      "-isystem" + rebase_path(
+              "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/sysroot/usr/include",
+              root_build_dir),
+      "-isystem" + rebase_path(
+              "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/sysroot/usr/include/arm-linux-androideabi",
+              root_build_dir),
+    ]
+
+    cflags_cc = [
+      "-isystem" + rebase_path(
+              "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/libcxx/include",
+              root_build_dir),
+      "-isystem" + rebase_path(
+              "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/libcxxabi/include",
+              root_build_dir),
+    ]
+
+    if (current_cpu == "arm") {
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_ndk_root/gcc/linux-x86/arm/gcc-linaro-7.5.0-arm-linux-gnueabi/arm-linux-gnueabi/lib",
+                       root_build_dir) ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_ndk_root/gcc/linux-x86/arm/gcc-linaro-7.5.0-arm-linux-gnueabi/lib/gcc/arm-linux-gnueabi/7.5.0",
+                       root_build_dir) ]
+      ldflags += [ "-l:libgcc.a" ]
+      ldflags +=
+          [ "-L" + rebase_path(
+                "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/libcxx/arm",
+                root_build_dir) ]
+      ldflags += [ "-l:libc++_static.a" ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_toolchain_root/lib64/clang/9.0.3/lib/linux",
+                       root_build_dir) ]
+      ldflags += [ "-l:libclang_rt.builtins-arm-android.a" ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/sdk/shared_library/arm",
+                       root_build_dir) ]
+      ldflags += [ "-l:liblog.so" ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/platforms/current/arch-arm/usr/lib",
+                       root_build_dir) ]
+    }
+
+    if (current_cpu == "arm64") {
+      ldflags += [ "-Wl,-z,max-page-size=4096" ]
+      ldflags += [ "-Wl,--hash-style=gnu" ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_ndk_root/gcc/linux-x86/arm/gcc-linaro-7.5.0-arm-linux-gnueabi/arm-linux-gnueabi/lib",
+                       root_build_dir) ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_ndk_root/gcc/linux-x86/arm/gcc-linaro-7.5.0-arm-linux-gnueabi/lib/gcc/arm-linux-gnueabi/7.5.0",
+                       root_build_dir) ]
+      ldflags += [ "-l:libgcc.a" ]
+      ldflags +=
+          [ "-L" + rebase_path(
+                "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/libcxx/arm64",
+                root_build_dir) ]
+      ldflags += [ "-l:libc++_static.a" ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_toolchain_root/lib64/clang/9.0.3/lib/linux",
+                       root_build_dir) ]
+      ldflags += [ "-l:libclang_rt.builtins-aarch64-android.a" ]
+      ldflags += [ "-L" + rebase_path(
+                       "//ohos_ndk_aosp/aosp_prebuilt_libs/asdk_libs/sdk/shared_library/arm64",
+                       root_build_dir) ]
+      ldflags += [ "-l:liblog.so" ]
+      ldflags += [ "-L" + rebase_path(
+                       "$ohos_ndk_root/aosp_prebuilt_libs/asdk_libs/ndk/platforms/current/arch-arm64/usr/lib",
+                       root_build_dir) ]
+    }
+
+    libs += [ "unwind_llvm" ]
+  }
+
+  # arm builds of libc++ starting in NDK r12 depend on unwind.
+  if (current_cpu == "arm") {
+    libs += [ "unwind" ]
+  }
+
+  # TODO(jdduke) Re-enable on mips after resolving linking
+  # issues with libc++ (crbug.com/456380).
+  if (current_cpu != "mipsel" && current_cpu != "mips64el") {
+    ldflags += [ "-Wl,--warn-shared-textrel" ]
+  }
+}
+
+config("lld_pack_relocations") {
+  if (use_musl) {
+    ldflags = [ "-Wl,--pack-dyn-relocs=relr" ]
+  } else {
+    ldflags = [ "-Wl,--pack-dyn-relocs=android" ]
+  }
+}
+
+# Used for instrumented build to generate the orderfile.
+config("default_orderfile_instrumentation") {
+  if (use_order_profiling) {
+    cflags = [ "-finstrument-function-entry-bare" ]
+    if (use_thin_lto) {
+      ldflags = [ "-Wl,-u,__cyg_profile_func_enter_bare" ]
+    }
+  }
+}
diff --git a/config/ohos/abi.gni b/config/ohos/abi.gni
new file mode 100644
index 000000000..34271e9e9
--- /dev/null
+++ b/config/ohos/abi.gni
@@ -0,0 +1,48 @@
+# Copyright 2022 The Huawei Authors. All rights reserved.
+# Use of this source code is governed by a BSD-style license that can be
+# found in the LICENSE file.
+
+# Logic separated out from config.gni so that it can be used by compiler.gni
+# without introducing a circular dependency.
+
+assert(is_ohos)
+
+if (current_cpu == "x86") {
+  ohos_app_abi = "x86"
+} else if (current_cpu == "arm") {
+  import("//build/config/arm.gni")
+  if (arm_version < 7) {
+    ohos_app_abi = "armeabi"
+  } else {
+    ohos_app_abi = "armeabi-v7a"
+  }
+} else if (current_cpu == "x64") {
+  ohos_app_abi = "x86_64"
+} else if (current_cpu == "arm64") {
+  ohos_app_abi = "arm64-v8a"
+} else {
+  assert(false, "Unknown ABI: " + current_cpu)
+}
+
+if (target_cpu == "arm64" || target_cpu == "x64") {
+  ohos_64bit_target_cpu = true
+} else if (target_cpu == "arm" || target_cpu == "x86") {
+  ohos_64bit_target_cpu = false
+} else {
+  assert(false, "Unknown target CPU: $target_cpu")
+}
+
+# Intentionally do not define ohos_app_secondary_abi_cpu and
+# ohos_app_secondary_abi for 32-bit target_cpu, since they are not used.
+if (target_cpu == "arm64") {
+  ohos_secondary_abi_cpu = "arm"
+  ohos_app_secondary_abi = "armeabi-v7a"
+} else if (target_cpu == "x64") {
+  ohos_secondary_abi_cpu = "x86"
+  ohos_app_secondary_abi = "x86"
+}
+
+if (defined(ohos_secondary_abi_cpu)) {
+  ohos_secondary_abi_cpu =
+      "//build/toolchain/ohos:ohos_clang_${ohos_secondary_abi_cpu}"
+}
diff --git a/config/ohos/config.gni b/config/ohos/config.gni
new file mode 100644
index 000000000..715543606
--- /dev/null
+++ b/config/ohos/config.gni
@@ -0,0 +1,139 @@
+# Copyright 2022 The Huawei Authors. All rights reserved.
+# Use of this source code is governed by a BSD-style license that can be
+# found in the LICENSE file.
+
+if (is_ohos) {
+  import("//build/config/dcheck_always_on.gni")
+  import("//build_overrides/build.gni")
+  import("abi.gni")
+
+  OHOS_NDK_HOME = getenv("OHOS_NDK_HOME")
+  declare_args() {
+    use_ohos_sdk_sysroot = false
+
+    # enable ohos bytrace, false by default
+    enable_ohos_bytrace = true
+
+    # Enable ohos_nweb_hap build target.
+    enable_ohos_nweb_hap = false
+  }
+
+  declare_args() {
+    arm32_ndk_api_level = 21
+    arm64_ndk_api_level = 21
+  }
+
+  declare_args() {
+    build_chromium_with_ohos_src = true
+    product_name = ""
+    gpu_switch = "on"
+  }
+
+  # Defines the name the ohos build gives to the current host CPU
+  # architecture, which is different than the names GN uses.
+  if (host_cpu == "x64") {
+    ohos_host_arch = "x86_64"
+  } else if (host_cpu == "x86") {
+    ohos_host_arch = "x86"
+  } else {
+    assert(false, "Need ohos toolchain support for your build CPU arch.")
+  }
+
+  # Defines the name the ohos build gives to the current host CPU
+  # architecture, which is different than the names GN uses.
+  if (host_os == "linux") {
+    ohos_host_os = "linux"
+  } else if (host_os == "mac") {
+    ohos_host_os = "darwin"
+  } else {
+    assert(false, "Need ohos toolchain support for your build OS.")
+  }
+
+  # config dependent paths
+  if (build_chromium_with_ohos_src) {
+    ohos_ndk_root = "//../../../prebuilts"
+    ohos_build_root = "//../../.."
+    if (use_musl) {
+      if (current_cpu == "x64") {
+        ohos_toolchain_root = "$ohos_ndk_root/clang/ohos/windows-x86_64/llvm"
+        ohos_sysroot = "$ohos_build_root/out/arm64/obj/third_party/musl"
+        ohos_libs_root = "$ohos_build_root/out/arm64/packages/phone/system/lib64"
+      } else {
+        ohos_toolchain_root = "$ohos_ndk_root/clang/ohos/linux-x86_64/llvm"
+        if (product_name == "rk3568") {
+          ohos_sysroot = "$ohos_build_root/out/rk3568/obj/third_party/musl"
+          if (current_cpu == "arm") {
+            ohos_libs_root = "$ohos_build_root/out/rk3568/packages/phone/system/lib"
+          } else {
+            ohos_libs_root = "$ohos_build_root/out/rk3568/packages/phone/system/lib64"
+          }
+        } else {
+          ohos_sysroot = "$ohos_build_root/out/generic_generic_arm_64only/hisi_newbaltimore_newphone_standard/obj/third_party/musl"
+          ohos_libs_root = "$ohos_build_root/out/generic_generic_arm_64only/hisi_newbaltimore_newphone_standard/packages/phone/system/lib64"
+        }
+      }
+      ohos_ndk_root = "${OHOS_NDK_HOME}"
+      ohos_toolchain_root = "$ohos_ndk_root/llvm"
+      ohos_sysroot = "$ohos_ndk_root/sysroot"
+
+      print("build config ohos config ohos_toolchain_root",ohos_toolchain_root)
+    } else {
+      assert(false, "Unsupport Non-musl target!")
+    }
+
+    # ohos include and libs dependencies
+    ohos_src_includes = [
+      "$ohos_build_root/base/hiviewdfx/hilog/interfaces/native/innerkits/include",
+      "$ohos_build_root/base/hiviewdfx/hisysevent/interfaces/native/innerkits/hisysevent/include/",
+      "$ohos_build_root/base/hiviewdfx/hitrace/interfaces/native/innerkits/include/hitrace_meter",
+      "$ohos_build_root/base/web/webview/ohos_adapter/interfaces",
+      "$ohos_build_root/commonlibrary/c_utils/base/include",
+      "$ohos_build_root/foundation/multimodalinput/input/interfaces/native/innerkits/event/include",
+    ]
+
+    ohos_libs_dir = [
+      "$ohos_libs_root",
+      "$ohos_libs_root/chipset-pub-sdk",
+      "$ohos_libs_root/platformsdk",
+    ]
+  } else {
+    if (use_musl) {
+      if (current_cpu == "x64") {
+        ohos_build_root = "${OHOS_NDK_HOME}"
+        ohos_ndk_root = "${OHOS_NDK_HOME}"
+      } else {
+        ohos_build_root = "${OHOS_NDK_HOME}"
+        ohos_ndk_root = "${OHOS_NDK_HOME}"
+      }
+      ohos_toolchain_root = "$ohos_ndk_root/toolchains/llvm"
+      print("build config ohos config ohos_toolchain_root 2",ohos_toolchain_root)
+      ohos_sysroot = "$ohos_ndk_root/sysroot"
+    } else {
+      assert(false, "Unsupport Non-musl target!")
+    }
+
+    if (use_ohos_sdk_sysroot) {
+      import("//ohos_sdk/ohos_sdk.gni")
+      ohos_sdk_root = "//ohos_sdk"
+      ohos_sysroot = "$ohos_sdk_root/native/$ohos_sdk_version/sysroot/"
+    }
+
+    # ohos include and libs dependencies, corresponding to gen_ohos_ndk.sh
+    ohos_src_includes = [
+      "$ohos_build_root/includes",
+      "$ohos_build_root/includes/hisysevent",
+      "$ohos_build_root/includes/hitrace_meter",
+      "$ohos_build_root/includes/multimodalinput",
+      "$ohos_build_root/includes/ohos_adapter",
+      "$ohos_build_root/includes/utils",
+    ]
+
+    if (current_cpu == "arm") {
+      ohos_libs_dir = [ "$ohos_build_root/libs" ]
+    } else if (current_cpu == "arm64") {
+      ohos_libs_dir = [ "$ohos_build_root/libs64" ]
+    } else if (current_cpu == "x64") {
+      ohos_libs_dir = [ "$ohos_build_root/libs64" ]
+    }
+  }
+}
diff --git a/config/sysroot.gni b/config/sysroot.gni
index 0f36edd9e..df736c026 100644
--- a/config/sysroot.gni
+++ b/config/sysroot.gni
@@ -34,6 +34,9 @@ if (sysroot == "") {
 
     # Android uses unified headers, and thus a single compile time sysroot
     sysroot = "$android_toolchain_root/sysroot"
+  } else if (is_ohos) {
+    import("//build/config/ohos/config.gni")
+    sysroot = "$ohos_sysroot"
   } else if ((is_linux || is_chromeos) && use_sysroot) {
     # By default build against a sysroot image downloaded from Cloud Storage
     # during gclient runhooks.
diff --git a/toolchain/ohos/BUILD.gn b/toolchain/ohos/BUILD.gn
new file mode 100644
index 000000000..a9a2e0e9c
--- /dev/null
+++ b/toolchain/ohos/BUILD.gn
@@ -0,0 +1,68 @@
+# Copyright 2022 The Huawei Authors. All rights reserved.
+# Use of this source code is governed by a BSD-style license that can be
+# found in the LICENSE file.
+
+import("//build/config/clang/clang.gni")
+import("//build/config/compiler/compiler.gni")
+import("//build/config/ohos/config.gni")
+import("//build/config/sysroot.gni")
+import("//build/toolchain/gcc_toolchain.gni")
+
+declare_args() {
+  # Whether unstripped binaries, i.e. compiled with debug symbols, should be
+  # considered runtime_deps rather than stripped ones.
+  ohos_unstripped_runtime_outputs = true
+}
+
+# The ohos clang toolchains share most of the same parameters, so we have this
+# wrapper around gcc_toolchain to avoid duplication of logic.
+#
+# Parameters:
+#  - binary_prefix
+#      Prefix of compiler executables.
+template("ohos_clang_toolchain") {
+  gcc_toolchain(target_name) {
+    assert(defined(invoker.toolchain_args),
+           "toolchain_args must be defined for ohos_clang_toolchain()")
+
+    toolchain_args = {
+      forward_variables_from(invoker.toolchain_args, "*")
+      current_os = target_os
+    }
+
+    # Output linker map files for binary size analysis.
+    enable_linker_map = true
+
+    _prefix = rebase_path("$clang_base_path/bin", root_build_dir)
+    cc = "$_prefix/clang"
+    cxx = "$_prefix/clang++"
+    ar = "$_prefix/llvm-ar"
+    ld = cxx
+    readelf = "$_prefix/llvm-readobj"
+    nm = "$_prefix/llvm-nm"
+    strip = rebase_path("$clang_base_path/bin/llvm-strip", root_build_dir)
+    use_unstripped_as_runtime_outputs = ohos_unstripped_runtime_outputs
+
+    # Don't use .cr.so for loadable_modules since they are always loaded via
+    # absolute path.
+    loadable_module_extension = ".so"
+  }
+}
+
+ohos_clang_toolchain("ohos_clang_arm") {
+  toolchain_args = {
+    current_cpu = "arm"
+  }
+}
+
+ohos_clang_toolchain("ohos_clang_arm64") {
+  toolchain_args = {
+    current_cpu = "arm64"
+  }
+}
+
+ohos_clang_toolchain("ohos_clang_x64") {
+  toolchain_args = {
+    current_cpu = "x64"
+  }
+}
